<?xml version="1.0"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009"
                       xmlns:s="library://ns.adobe.com/flex/spark"
                       xmlns:mx="library://ns.adobe.com/flex/mx"
                       xmlns:local="*"
                       xmlns:tabBar="tabbar.*"
                       showStatusBar="false"
                       creationComplete="creationCompleteHandler(event)"
                       close="closeHandler(event)"
        >
    <fx:Script><![CDATA[
        import mx.collections.ArrayCollection;
        import mx.events.FileEvent;
        import mx.events.FlexEvent;

        import spark.events.IndexChangeEvent;

        public static var HOME_FOLDER_PATH:String = "root$:\\Computer";

        public var files:Dictionary = new Dictionary();

        [Bindable]
        public var tabs:ArrayCollection = new ArrayCollection();

        public function setIconsForSelected(iconString:String):void {
            for each (var f:File in fileList.selectedItems) {
                files[ f.nativePath ] = iconString;
            }
            fileList.refresh();
            saveDictionary();
        }

        private function handleFolderChange():void {
            output.text = fileList.directory.nativePath;
            if (tabBar.selectedIndex != -1) {
                tabs.setItemAt(output.text, tabBar.selectedIndex);
            }
        }

        private function fileList_fileChooseHandler(event:FileEvent):void {
            event.file.openWithDefaultApplication();
            files[ event.file.nativePath ] = IconClasses.questionIconName;
            fileList.refresh();
        }

        private function closeHandler(event:Event):void {
            saveLocation();
            saveDictionary();
        }

        public function loadLocation():void {
            var file:File = File.applicationStorageDirectory;
            file = file.resolvePath("location.xml");
            if (file.exists) {
                var xml:XML = new CsUtils().loadXML(file.url);
                if (xml != null) {
                    for each (var node:XML in xml.paths.path) {
                        var path:String = node.path;
                        if (path != HOME_FOLDER_PATH) {
                            tabs.addItem(path);
                        }
                    }
                }
            }
            if (tabs.length == 0) {
                tabs.addItem(HOME_FOLDER_PATH);
            }
        }

        public function saveLocation():void {
            var file:File = File.applicationStorageDirectory;
            file = file.resolvePath("location.xml");
            var xml:XML = new XML("<location><paths/></location>");
            for each (var tab:Object in tabs) {
                var node:XML = new XML("<path/>");
                node.path = tab;
                (xml.paths as XMLList).appendChild(node);
            }
            new CsUtils().saveAsXML(xml, file.url);
        }

        public function saveDictionary():void {
            var file:File = File.applicationStorageDirectory;
            file = file.resolvePath("dictionary.xml");
            var xml:XML = new XML("<dictionary><items/></dictionary>");
            for (var key:Object in files) {
                var node:XML = new XML("<item><path/><iconString/></item>");
                node.path = key;
                node.iconString = files[key];
                (xml.items as XMLList).appendChild(node);
            }
            new CsUtils().saveAsXML(xml, file.url);
        }

        public function loadDictionary():void {
            var file:File = File.applicationStorageDirectory;
            file = file.resolvePath("dictionary.xml");
            if (file.exists) {
                var xml:XML = new CsUtils().loadXML(file.url);
                if (xml != null) {
                    for each (var node:XML in xml.items.item) {
                        files[String(node.path)] = String(node.iconString);
                    }
                }
            }
        }

        private function creationCompleteHandler(event:FlexEvent):void {

            loadLocation();
            tabBar.setSelectedToLast();
            fileList.directory = new File(tabBar.selectedItem);
            loadDictionary();

        }

        private function tabBar1_changeHandler(event:IndexChangeEvent):void {
            fileList.directory = new File(tabBar.selectedItem);
        }

        private static function about_clickHandler(event:MouseEvent):void {
            var urlReq:URLRequest = new URLRequest("https://github.com/csomakk/IWatchSeriez/");
            navigateToURL(urlReq, "_blank");
        }
        ]]></fx:Script>
    <s:VGroup paddingBottom="10" paddingLeft="10" paddingTop="10" paddingRight="10"
              width="100%" height="100%">
        <s:HGroup width="100%">
            <s:Button label="Up" click="fileList.navigateUp(); handleFolderChange();"
                      enabled="{ fileList.canNavigateUp }"/>
            <mx:FileSystemHistoryButton label="Back" dataProvider="{fileList.backHistory}"
                                        enabled="{fileList.canNavigateBack}"
                                        click="fileList.navigateBack();"
                                        itemClick="fileList.navigateBack(event.index);"/>
            <mx:FileSystemHistoryButton label="Forward" dataProvider="{fileList.forwardHistory}"
                                        enabled="{fileList.canNavigateForward}"
                                        click="fileList.navigateForward();"
                                        itemClick="fileList.navigateForward(event.index);"/>
            <s:TextInput id="output" width="100%"/>
        </s:HGroup>
        <tabBar:TabBar id="tabBar"
                       left="8" right="8" y="2"
                       data="@{tabs}"
                       change="tabBar1_changeHandler(event)"/>
        <mx:FileSystemList width="100%" height="100%"
                           id="fileList"
                           fileChoose="fileList_fileChooseHandler(event);"
                           directoryChange="handleFolderChange();"
                           itemRenderer="FileSystemListItemRenderer"
                           allowMultipleSelection="true"
                />
        <local:IconsBar topLevelApp="{this}"/>
    </s:VGroup>
    <s:Image bottom="8" right="8" width="32" height="32"
             smooth="true"
             source="{ IconClasses.githubIcon }"
             toolTip="Help us! Contribute on GitHub!"
             click="about_clickHandler(event)"/>
</s:WindowedApplication>
